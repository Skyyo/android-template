plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint' version '10.3.0'
    id 'io.gitlab.arturbosch.detekt' version '1.21.0-RC1'
    id 'dagger.hilt.android.plugin'
}
android {
    compileSdk 33
    defaultConfig {
        applicationId "com.skyyo.template"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def homePath = System.getProperty("user.home")
            def tmpFilePath = homePath + "/work/_temp/keystore/"
            def allFilesFromDir = new File(tmpFilePath).listFiles()
            if (allFilesFromDir != null) {
                def keystoreFile = allFilesFromDir.first()
                keystoreFile.renameTo("keystore/keystore.jks")
            }
            storeFile = file(homePath + "/work/_temp/keystore/keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://release.com/\""
        }
        internalTestTrack {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://internal.com/\""
        }
        debug {
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://debug.com/\""
        }
    }
    kotlinOptions {
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion "1.3.0"
    }
}
detekt {
    config = files("$rootDir/.detekt/config.yml")
}
ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = false
    enableExperimentalRules = false
    disabledRules = ["no-wildcard-imports", "max-line-length", "import-ordering"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}
dependencies {

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.3'

    implementation "androidx.appcompat:appcompat:1.6.0-beta01"
    implementation 'androidx.core:core-ktx:1.9.0-beta01'
    implementation "androidx.fragment:fragment-ktx:1.5.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:2.6.0-alpha01"


    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"

    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"

    implementation("io.coil-kt:coil-compose:2.1.0")

    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation 'androidx.core:core-splashscreen:1.0.0'

    implementation("com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2")
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation("androidx.hilt:hilt-navigation-fragment:1.0.0") {
        exclude group: 'androidx.navigation'
    }

    implementation 'com.google.android.material:material:1.6.1'
    implementation "dev.chrisbanes.insetter:insetter:0.6.0"

    debugImplementation "com.github.YarikSOffice.Venom:venom:0.5.0"
    releaseImplementation "com.github.YarikSOffice.Venom:venom-no-op:0.5.0"

    testImplementation "io.mockk:mockk:1.12.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'
    testImplementation 'app.cash.turbine:turbine:0.7.0'
    testImplementation 'junit:junit:4.13.2'
}