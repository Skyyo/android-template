plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint' version '9.3.0'
    id 'io.gitlab.arturbosch.detekt' version '1.15.0'
    id 'dagger.hilt.android.plugin'
    id "com.google.protobuf" version "0.8.12"
}
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.skyyo.template"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"https://release.com/\""
        }
        debug {
            minifyEnabled false
            buildConfigField "String", "BASE_URL", "\"https://debug.com/\""
        }
    }
    buildFeatures.viewBinding = true
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}
detekt {
    config = files("$rootDir/.detekt/config.yml")
}
ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = false
    enableExperimentalRules = false
    disabledRules = ["no-wildcard-imports", "max-line-length", "import-ordering"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}
dependencies {
    testImplementation 'junit:junit:4.13.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.3'

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation "androidx.fragment:fragment-ktx:1.3.3"
    implementation 'androidx.core:core-ktx:1.6.0-alpha02'
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.4.0-alpha01'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.datastore:datastore-preferences:1.0.0-beta01"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    implementation("com.squareup.okhttp3:logging-interceptor:4.9.1")
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"

    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    implementation("androidx.hilt:hilt-navigation-fragment:1.0.0-beta01") {
        exclude group: 'androidx.navigation'
    }

    implementation 'com.google.android.material:material:1.3.0'
    implementation "com.xwray:groupie:$groupie_version"
    implementation "com.xwray:groupie-viewbinding:$groupie_version"
    implementation "dev.chrisbanes.insetter:insetter:0.5.0"
    implementation('io.coil-kt:coil:1.1.1')

    //datastore
    implementation "androidx.datastore:datastore:1.0.0-alpha08"
    implementation "com.google.protobuf:protobuf-javalite:3.10.0"

    debugImplementation "com.github.YarikSOffice.Venom:venom:0.3.1"
    releaseImplementation "com.github.YarikSOffice.Venom:venom-no-op:0.3.1"
}
// Generates the java Protobuf-lite code for the Protobufs in this project.
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}